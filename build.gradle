
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "org.frege-lang:frege-gradle-plugin:0.4.1"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "frege"
apply plugin: 'io.codearte.nexus-staging'
apply plugin: "application"

tasks.compileFrege.dependsOn "compileJava"

compileFrege {
    verbose = true
    hints = true
    make = true
    inline = false
}

mainClassName = "frege.nativegen.Main"
applicationName = "fregeffi"

run{
    standardInput = System.in
    standardOutput = System.out
    if (hasProperty("args"))
        args project.args.split()
}

defaultTasks "build"

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    baseVersion = "1.3.1"
    isSnapshot = true
    snapshotAppendix = "-SNAPSHOT"
    projectVersion = baseVersion + (isSnapshot ? snapshotAppendix : "")

    fregeBaseVersion = "3.22.524"
    fregeClassifier = "-gcc99d7e"
    fregeVersion = "$fregeBaseVersion$fregeClassifier"

    guavaVersion = "18.0"
}

apply from: "$rootDir/gradle/sonatype.gradle"

version = projectVersion
group = groupName
archivesBaseName = project.name

repositories {
    mavenLocal()
    maven {
        url = sonatypeRepositoryUrl
    }
    mavenCentral()
}

dependencies {
    compile "org.frege-lang:frege:$fregeVersion"
    compile "com.google.guava:guava:$guavaVersion"
}

nexusStaging {
    packageGroup = groupName
    stagingProfileId = "a622b6773bea07"
}
